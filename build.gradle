import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.graalvm.buildtools.gradle.tasks.BuildNativeImageTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.graalvm.buildtools.native' version '0.9.23'
	id 'jacoco'
	id "org.sonarqube" version "3.5.0.2730"
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'it.pagopa.cstar'
version = '1.0.0'

java {
	sourceCompatibility = '17'
}

ext {
	jwtVersion = "0.11.5"
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		// from https://github.com/open-telemetry/opentelemetry-java-examples/blob/main/spring-native/build.gradle
		mavenBom 'io.opentelemetry:opentelemetry-bom:1.30.1'
		mavenBom 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:1.30.0-alpha'
		mavenBom 'io.micrometer:micrometer-tracing-bom:1.1.4'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"

	// span and traces
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter"
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-webflux-5.3"

	// https://mvnrepository.com/artifact/com.azure/azure-monitor-opentelemetry-exporter
	implementation 'com.azure:azure-monitor-opentelemetry-exporter:1.0.0-beta.11' // allows to export to azure

	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"
	runtimeOnly "io.opentelemetry.instrumentation:opentelemetry-logback-appender-1.0"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/reports/jacoco.xml")
	}
}

sonarqube {
	properties {
		property "sonar.exclusions", "**/it.pagopa.cstar.iomock.config/**"
	}
}

test {
    useJUnitPlatform()
	finalizedBy jacocoTestReport
}

graalvmNative {
    metadataRepository {
        enabled = true
        version = "0.3.4"
    }
	binaries {
        useArgFile.set(false)

        main {
            useFatJar = true
            buildArgs.add("--initialize-at-run-time=io.netty")
			buildArgs.add("--initialize-at-build-time=org.slf4j.LoggerFactory,ch.qos.logback")
        }
    }
}

tasks.named("shadowJar", ShadowJar.class) {
	zip64 = true
	archiveBaseName.set('shadow')
	duplicatesStrategy = DuplicatesStrategy.WARN
	mergeServiceFiles()

	manifest {
		attributes 'Main-Class': 'it.pagopa.cstar.iomock.IoMockApplication'
	}

	from(graalvmNative.binaries.named('main').get().classpath.getElements().map {
		it.stream().map { if (it.asFile.path.endsWith(".jar")) zipTree(it) else it }.toList()
	})
}

tasks.named("nativeCompile", BuildNativeImageTask.class) {
	final shadowJarTask = tasks.named("shadowJar", ShadowJar.class).get()
	it.dependsOn(shadowJarTask)
	it.classpathJar.set(shadowJarTask.archiveFile.get())
}