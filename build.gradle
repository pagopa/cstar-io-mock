plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.graalvm.buildtools.native' version '0.9.27'
	id 'jacoco'
	id "org.sonarqube" version "3.5.0.2730"
}

group = 'it.pagopa.cstar'
version = '1.1.1'

java {
	sourceCompatibility = '17'
}

// properties
ext {
	jwtVersion = "0.11.5"
}

ext.set("applicationinsights.enabled", true)

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		// from https://github.com/open-telemetry/opentelemetry-java-examples/blob/main/spring-native/build.gradle
		mavenBom 'io.opentelemetry:opentelemetry-bom:1.31.0'
		mavenBom 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:1.31.0-alpha'
		mavenBom 'io.micrometer:micrometer-tracing-bom:1.1.4'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

	// span and traces
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter"
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-webflux-5.3"

	// https://mvnrepository.com/artifact/com.azure/azure-monitor-opentelemetry-exporter
	implementation 'com.azure:azure-monitor-opentelemetry-exporter:1.0.0-beta.11' // allows to export to azure

	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"
	runtimeOnly "io.opentelemetry.instrumentation:opentelemetry-logback-appender-1.0"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/reports/jacoco.xml")
	}
}

sonarqube {
	properties {
		property "sonar.exclusions", "**/it.pagopa.cstar.iomock.config/**"
	}
}

test {
    useJUnitPlatform()
	finalizedBy jacocoTestReport
}


graalvmNative {
    metadataRepository {
        enabled = true
        version = "0.3.5"
    }
	binaries {
        useArgFile.set(false)

        main {
            useFatJar = true
            buildArgs.add("--initialize-at-run-time=io.netty")
			buildArgs.add("--initialize-at-build-time=org.slf4j.LoggerFactory,ch.qos.logback")
        }
    }
}

nativeCompileClasspathJar {
	exclude 'META-INF/*.RSA'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
}